# # Star tmux
# # if (not in tmux) and (interactive env) and (not embedded terminal)
# [[ -z $TMUX && $- == *i* && ! $(</proc/$PPID/cmdline) =~ "dolphin|vim|emacs|code" ]] && tmux

# Internal functions
__source() {  # source if exist
    # -f: file exists and readable
    [[ -f $1 ]] && source "$1"
}

# Start p10k instant prompt
__source "$XDG_CACHE_HOME/p10k-instant-prompt-${(%):-%n}.zsh"

# Load plugins
source "$HOME/.zgenom/zgenom.zsh"
if ! zgenom saved; then
    zgenom ohmyzsh lib/completion.zsh
    zgenom ohmyzsh lib/key-bindings.zsh
    zgenom ohmyzsh lib/clipboard.zsh

    zgenom ohmyzsh plugins/extract
    zgenom ohmyzsh plugins/pip
    # zgenom ohmyzsh plugins/ripgrep
    # zgenom ohmyzsh plugins/rust
    # zgenom ohmyzsh plugins/docker
    # zgenom ohmyzsh plugins/docker-compose

    zgenom load romkatv/powerlevel10k powerlevel10k
    zgenom load junegunn/fzf shell/key-bindings.zsh
    zgenom load Aloxaf/fzf-tab
    zgenom load zdharma-continuum/fast-syntax-highlighting
    zgenom load zsh-users/zsh-autosuggestions
    zgenom load zsh-users/zsh-history-substring-search
    zgenom load hlissner/zsh-autopair

    zgenom save
fi

# TODO: Add completions for rustup and cargo by `rustup completions`

# Configure zsh misc
setopt auto_cd               # simply type dir name to cd
setopt auto_pushd            # make cd behaves like pushd
setopt pushd_ignore_dups     # don't pushd duplicates
setopt pushd_minus           # exchange the meanings of `+` and `-` in pushd
setopt interactive_comments  # comments in interactive shells
setopt multios               # multiple redirections
setopt ksh_option_print      # make setopt output all options
WORDCHARS='*?.[]~-_=&;!#$%^(){}<>'

# Configure zsh history
setopt hist_ignore_all_dups  # no duplicates
setopt hist_save_no_dups     # don't save duplicates
setopt hist_ignore_space     # no commands starting with space
setopt hist_reduce_blanks    # remove all unneccesary spaces
setopt sharehistory          # share history between sessions
setopt no_bang_hist          # no history expansion
HISTFILE="$HOME/.zsh_history"
HISTSIZE=50000
SAVEHIST=10000

# Configure zsh completion
_complete_global_alias() {
    # Modified from https://stackoverflow.com/questions/59512364
    # ${(k)galiases}: keys of $galiases
    [[ $PREFIX == :* ]] && compadd -- ${(k)galiases}
    return 1
}
zstyle ':completion:*' completer _complete_global_alias _complete _ignored
zstyle ':completion:*:git-checkout:*' sort false
zstyle ':completion:*:git-rebase:*' sort false

# General env variables
MY_PROXY="127.0.0.1:1999"

# # Configure starship
# __my_starship_precmd() {
#     # Set window title
#     echo -ne "\033]0; $(basename "$PWD") \007"
#     # Set my pwd
#     export MY_STARSHIP_PWD="${(D):-$PWD}"
# }
# precmd_functions+=(__my_starship_precmd)

# Configure fzf
export FZF_DEFAULT_OPTS="--ansi --height=60% --reverse --cycle --bind=tab:accept"

# Configure fzf-tab
zstyle ':fzf-tab:*' fzf-command ftb-tmux-popup
zstyle ':fzf-tab:*' fzf-bindings 'tab:accept'
zstyle ':fzf-tab:complete:kill:argument-rest' fzf-preview 'ps --pid=$word -o cmd --no-headers -w -w'
zstyle ':fzf-tab:complete:kill:argument-rest' fzf-flags '--preview-window=down:3:wrap'
zstyle ':fzf-tab:complete:kill:*' popup-pad 0 3

# Configure fast-syntax-highlighting
export FAST_HIGHLIGHT[chroma-man]=""  # chroma-man will stuck history browsing

# Configure bat
export BAT_THEME="OneHalfDark"

# Configure man-pages
export MANPAGER="sh -c 'col -bx | bat -l man -p --theme=\"Monokai Extended\"'"
export MANROFFOPT="-c"

# Configure rustup
export RUSTUP_DIST_SERVER="https://rsproxy.cn"
export RUSTUP_UPDATE_ROOT="https://rsproxy.cn/rustup"

# Configure npm
export NPM_CONFIG_PREFIX="$HOME/.local"
export NPM_CONFIG_CACHE="$XDG_CACHE_HOME/npm"
export NPM_CONFIG_PROXY="$MY_PROXY"

# Key bindings
bindkey '^H' backward-kill-word  # [Ctrl-Backspace/H]
bindkey '^Z' undo                # [Ctrl-Z]
bindkey '^Y' redo                # [Ctrl-Y]
bindkey -s '^N' '^Q cd "${$(xplr):-.}" \n'  # [Ctrl-N] run xplr to navigate

# Aliases
alias l="exa -lah --group-directories-first --git --time-style=long-iso"
alias lt="l -TI '.git'"
alias clc="clipcopy"   # from OMZL::clipboard.zsh
alias clp="clippaste"  # from OMZL::clipboard.zsh
alias sc="sudo systemctl"
alias scu="systemctl --user"
alias sudo="sudo "
alias cgp="cgproxy "
alias pc="proxychains -q "
alias open="xdg-open"
alias with-proxy=' \
    http_proxy=$MY_PROXY \
    HTTP_PROXY=$MY_PROXY \
    https_proxy=$MY_PROXY \
    HTTPS_PROXY=$MY_PROXY '
alias -g :bg="&>/dev/null &"
alias -g :bg!="&>/dev/null &!"

# Directory shortcuts
hash -d config="$XDG_CONFIG_HOME"
hash -d cache="$XDG_CACHE_HOME"
hash -d data="$XDG_DATA_HOME"
hash -d Downloads="$HOME/Downloads"
hash -d Workspace="$HOME/Workspace"
hash -d Temp="$HOME/Workspace/Code/Temp"
hash -d NotMe="$HOME/Workspace/Code/NotMe"
hash -d QuarticCat="$HOME/Workspace/Code/QuarticCat"
hash -d Homework="$HOME/Workspace/Homework"
hash -d OneDrive="$HOME/OneDrive"

# My functions
f() {  # Some fzf utilities
    case $1 in
    doc) (
        cd ~OneDrive/Documents
        selected="$(fd --type=file | fzf)" &&
        okular "$selected" :bg!
    )   ;;
    hw) (
        cd ~Homework
        selected="$(fd --type=directory --max-depth=2 | fzf)" &&
        dolphin "$selected" :bg!
    )   ;;
    *)
        echo "Unknown argument!" >&2
        return 1
        ;;
    esac
}
nbnhhsh() {
    # "'$*'": join arguments into a single string with spaces
    pc xh --body post https://lab.magiconch.com/api/nbnhhsh/guess text="'$*'"
}
reboot-to-windows() {
    # Modified from https://unix.stackexchange.com/questions/43196
    local windows_title="$(sudo rg -i windows /boot/grub/grub.cfg | cut -d "'" -f 2)"
    sudo grub-reboot "$windows_title" && sudo reboot
}
# rfv() {
#     # Modified from https://github.com/junegunn/fzf/blob/master/ADVANCED.md#using-fzf-as-the-secondary-filter
#     # Waiting this to be fixed https://github.com/sharkdp/bat/issues/1185
#     rg_prefix="rg --column --line-number --no-heading --color=always --smart-case "
#     IFS=: read -rA selected < =(
#         FZF_DEFAULT_COMMAND="$rg_prefix $1" \
#         fzf --disabled --query "$1" \
#             --bind "change:reload:sleep 0.1; $rg_prefix {q} || true" \
#             --delimiter : \
#             --preview "bat --color=always {1} --highlight-line {2} --terminal-width $(tput cols)" \
#             --preview-window "up,60%,border-bottom,~3"
#     )
#     [[ -n $selected[1] ]] && vim "$selected[1]" "+$selected[2]"
# }
# rfv2() {
#     # Modified from https://github.com/junegunn/fzf/blob/master/ADVANCED.md#using-fzf-as-interative-ripgrep-launcher
#     # Waiting this to be fixed https://github.com/sharkdp/bat/issues/1185
#     IFS=: read -rA selected < =(
#         rg --color=always --line-number --no-heading --smart-case "$1" |
#         fzf --color "hl:-1:underline,hl+:-1:underline:reverse" \
#             --delimiter : \
#             --preview "bat --color=always {1} --highlight-line {2} --terminal-width $(tput cols)" \
#             --preview-window "up,60%,border-bottom,~3"
#     )
#     [[ -n $selected[1] ]] && vim "$selected[1]" "+$selected[2]"
# }

# Load scripts
eval "$(direnv hook zsh)"
__source "$HOME/.opam/opam-init/init.zsh"
__source "$HOME/.p10k.zsh"
# eval "$(starship init zsh)"
